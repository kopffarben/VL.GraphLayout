shader ImposterID_DrawFX : VS_PS_Base
{
    stage StructuredBuffer<bool> enabledBuffer;
    StructuredBuffer<float3> posBuffer;
    StructuredBuffer<float> sizeBuffer;


    // SPRITES UTILS ==============================================================
 
    void CircleSpriteDiscard(float2 uv)
    {
        if(length(uv - 0.5f) > 0.5f)
        {
            discard;
        }
    }

    static const float3 QuadPositions[4] = 
    {
        float3(-1, 1,0),
        float3( 1, 1,0),
        float3(-1, -1,0),
        float3( 1, -1,0),
    };

    static const float2 QuadUV[4] = 
    {
        float2(0,1), 
        float2(1,1),
        float2(0,0),
        float2(1,0)
    };


    cbuffer PerFrame
    {
        float sizeDefualt;
    };

    stream bool Enabled;
    stream float2 TexCoord;
    stream uint VertexID : SV_VertexID;
    stream float4 Color;
    stream float Size;

    // VS ==============================================================================
    stage override void VSMain() 
    {
        uint id = streams.VertexID;
             
        streams.Enabled = enabledBuffer[id];
        streams.PositionWS = float4(posBuffer[id], 1);

        uint count, dummy;	
        sizeBuffer.GetDimensions(count, dummy);
        streams.Size = count > 0 ? sizeBuffer[id % count] * sizeDefualt : sizeDefualt;
        streams.Color = float4((float)id,(float)id,-1,-1);
    }

    // GS ==============================================================================
    [maxvertexcount(4)]
    stage void GSMain(point Input input[1], inout TriangleStream<Output> triangleStream)
    {
        streams = input[0];

        if (streams.Enabled)
        {
            for(int i=0; i<4; i++)
            {
                streams.TexCoord  = QuadUV[i].xy;
            
                float4 posView = mul(streams.PositionWS, WorldView);
                posView.xyz += QuadPositions[i].xyz *  streams.Size;
                streams.ShadingPosition = mul(posView, Projection);
                triangleStream.Append(streams);
            }
        }
    }


    // PS ==============================================================================
    stage override void PSMain()
    {
        CircleSpriteDiscard(streams.TexCoord);       
        streams.ColorTarget = streams.Color;
    }

};